def config = ["--android", "--editorconfig=$rootProject.rootDir", "--experimental"]
def files = "src/**/*.kt"
def ktlint_version = "0.36.0"
def style = ["--color",  "--color-name=RED"]

repositories {
    jcenter()
}

subprojects {

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:$ktlint_version"
    }

    task ktlint(type: JavaExec, group: "quality") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args config
        args style
        args "--reporter=plain?group_by_file"
        args files
    }

    task ktlintFormat(type: JavaExec, group: "quality") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args config
        args style
        args "-F"
        args files
    }
}